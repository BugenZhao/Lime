var max = |a, b| {
    if a > b { a; } else { b; }
};
var min = |a, b| {
    if a < b { a; } else { b; }
};
var add = |a, b| { a + b; };
var mul = |a, b| { a * b; };

var id = |x| { x; };

var assert_eq = |a, b| {
    if a &!= b { panic("Assertion failed:\n  left:", dbg(a), "\n right:", dbg(b)); }
    true;
};

class Pair { a, b }

impl Vec {
    assoc iter = |self| {
        VecIter {
            vec: self,
            curr: 0,
            len: self.len(),
        };
    };

    assoc map = |self, fn| {
        var ret = [];
        for e in self {
            ret.push(fn(e));
        }
        ret;
    };

    assoc fold = |self, initial, fn| {
        var acc = initial;
        for e in self {
            acc = fn(acc, e);
        }
        acc;
    };
}

class VecIter { vec, curr, len }

impl VecIter {
    assoc iter = |self| { self; };

    assoc next = |self| {
        if self.curr >= self.len { nil.expect("stop iteration"); }
        else {
            var ans = self.vec.get(self.curr);
            self.curr = self.curr + 1;
            ans;
        }
    };
}
